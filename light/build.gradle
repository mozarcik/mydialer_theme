buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.1.0'
    }
}
apply plugin: 'com.android.application'

repositories {
    mavenCentral()
}

def getVersionName = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe', '--tags', '--dirty'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

def getVersionCode = { ->
    def code = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-list', 'HEAD', '--count'
        standardOutput = code
    }
    return Integer.parseInt(code.toString().trim())
}
android {
    compileSdkVersion 21
    buildToolsVersion "21.1.2"

    defaultConfig {
        versionCode getVersionCode()
        versionName getVersionName()
        minSdkVersion 15
        targetSdkVersion 21
    }

    signingConfigs {
        release {
            storeFile file(sign_store_file)
            storePassword sign_store_password
            keyAlias sign_key_alias
            keyPassword sign_key_password
        }
    }
    buildTypes {
        release {
            signingConfig signingConfigs.release
         }
    }
}

dependencies {
}
